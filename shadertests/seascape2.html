<html>
<head>
<title>Shader</title>
</head>
<body>

<p>
<canvas id="canvas" width="320" height="64"></canvas>
</p>

<p>
<canvas id="c1" width="1000" height="500"></canvas>
</p>

<p>
<span id="wsdi_status">Not initialized</span>
<span id="number"></span>
<button id="startstop">start / stop animation</button>
</p>
<p>
send <input id="scanlines" value="3"></input> every
<input id="scaninterval" value="20"></input> milliseconds.
</p>

<script src="toyrunner.min.js" type="text/javascript"></script>
<script src="../ledpanel.js" type="text/javascript"></script>

<div id="errormessage"></div>
<script id="2d-vertex-shader" type="x-shader/x-vertex">

precision mediump float;
attribute vec2 position;

void main() {
  gl_Position = vec4(position.x, position.y, 0, 1);
}

</script>
<script id="2d-fragment-shader" type="x-shader/x-fragment">




// "Seascape" by Alexander Alekseev aka TDM - 2014
// License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

const int NUM_STEPS = 8;
const float PI	 	= 3.1415;
const float EPSILON	= 1e-3;
float EPSILON_NRM	= 0.1 / iResolution.x;

// sea
const int ITER_GEOMETRY = 3;
const int ITER_FRAGMENT = 5;
const float SEA_HEIGHT = 0.6;
const float SEA_CHOPPY = 4.0;
const float SEA_SPEED = 0.8;
const float SEA_FREQ = 0.16;
const vec3 SEA_BASE = vec3(0.1,0.19,0.22);
const vec3 SEA_WATER_COLOR = vec3(0.8,0.9,0.6);
float SEA_TIME = iGlobalTime * SEA_SPEED;
mat2 octave_m = mat2(1.6,1.2,-1.2,1.6);

// math
mat3 fromEuler(vec3 ang) {
	vec2 a1 = vec2(sin(ang.x),cos(ang.x));
    vec2 a2 = vec2(sin(ang.y),cos(ang.y));
    vec2 a3 = vec2(sin(ang.z),cos(ang.z));
    mat3 m;
    m[0] = vec3(a1.y*a3.y+a1.x*a2.x*a3.x,a1.y*a2.x*a3.x+a3.y*a1.x,-a2.y*a3.x);
	m[1] = vec3(-a2.y*a1.x,a1.y*a2.y,a2.x);
	m[2] = vec3(a3.y*a1.x*a2.x+a1.y*a3.x,a1.x*a3.x-a1.y*a3.y*a2.x,a2.y*a3.y);
	return m;
}
float hash( vec2 p ) {
	float h = dot(p,vec2(127.1,311.7));	
    return fract(sin(h)*43758.5453123);
}
float noise( in vec2 p ) {
    vec2 i = floor( p );
    vec2 f = fract( p );	
	vec2 u = f*f*(3.0-2.0*f);
    return -1.0+2.0*mix( mix( hash( i + vec2(0.0,0.0) ), 
                     hash( i + vec2(1.0,0.0) ), u.x),
                mix( hash( i + vec2(0.0,1.0) ), 
                     hash( i + vec2(1.0,1.0) ), u.x), u.y);
}

// lighting
float diffuse(vec3 n,vec3 l,float p) {
    return pow(dot(n,l) * 0.4 + 0.6,p);
}
float specular(vec3 n,vec3 l,vec3 e,float s) {    
    float nrm = (s + 8.0) / (3.1415 * 8.0);
    return pow(max(dot(reflect(e,n),l),0.0),s) * nrm;
}

// sky
vec3 getSkyColor(vec3 e) {
    e.y = max(e.y,0.0);
    vec3 ret;
    ret.x = pow(1.0-e.y,2.0);
    ret.y = 1.0-e.y;
    ret.z = 0.6+(1.0-e.y)*0.4;
    return ret;
}

// sea
float sea_octave(vec2 uv, float choppy) {
    uv += noise(uv);        
    vec2 wv = 1.0-abs(sin(uv));
    vec2 swv = abs(cos(uv));    
    wv = mix(wv,swv,wv);
    return pow(1.0-pow(wv.x * wv.y,0.65),choppy);
}

float map(vec3 p) {
    float freq = SEA_FREQ;
    float amp = SEA_HEIGHT;
    float choppy = SEA_CHOPPY;
    vec2 uv = p.xz; uv.x *= 0.75;
    
    float d, h = 0.0;    
    for(int i = 0; i < ITER_GEOMETRY; i++) {        
    	d = sea_octave((uv+SEA_TIME)*freq,choppy);
    	d += sea_octave((uv-SEA_TIME)*freq,choppy);
        h += d * amp;        
    	uv *= octave_m; freq *= 1.9; amp *= 0.22;
        choppy = mix(choppy,1.0,0.2);
    }
    return p.y - h;
}

float map_detailed(vec3 p) {
    float freq = SEA_FREQ;
    float amp = SEA_HEIGHT;
    float choppy = SEA_CHOPPY;
    vec2 uv = p.xz; uv.x *= 0.75;
    
    float d, h = 0.0;    
    for(int i = 0; i < ITER_FRAGMENT; i++) {        
    	d = sea_octave((uv+SEA_TIME)*freq,choppy);
    	d += sea_octave((uv-SEA_TIME)*freq,choppy);
        h += d * amp;        
    	uv *= octave_m; freq *= 1.9; amp *= 0.22;
        choppy = mix(choppy,1.0,0.2);
    }
    return p.y - h;
}

vec3 getSeaColor(vec3 p, vec3 n, vec3 l, vec3 eye, vec3 dist) {  
    float fresnel = 1.0 - max(dot(n,-eye),0.0);
    fresnel = pow(fresnel,3.0) * 0.65;
        
    vec3 reflected = getSkyColor(reflect(eye,n));    
    vec3 refracted = SEA_BASE + diffuse(n,l,80.0) * SEA_WATER_COLOR * 0.12; 
    
    vec3 color = mix(refracted,reflected,fresnel);
    
    float atten = max(1.0 - dot(dist,dist) * 0.001, 0.0);
    color += SEA_WATER_COLOR * (p.y - SEA_HEIGHT) * 0.18 * atten;
    
    color += vec3(specular(n,l,eye,60.0));
    
    return color;
}

// tracing
vec3 getNormal(vec3 p, float eps) {
    vec3 n;
    n.y = map_detailed(p);    
    n.x = map_detailed(vec3(p.x+eps,p.y,p.z)) - n.y;
    n.z = map_detailed(vec3(p.x,p.y,p.z+eps)) - n.y;
    n.y = eps;
    return normalize(n);
}

float heightMapTracing(vec3 ori, vec3 dir, out vec3 p) {  
    float tm = 0.0;
    float tx = 1000.0;    
    float hx = map(ori + dir * tx);
    if(hx > 0.0) return tx;   
    float hm = map(ori + dir * tm);    
    float tmid = 0.0;
    for(int i = 0; i < NUM_STEPS; i++) {
        tmid = mix(tm,tx, hm/(hm-hx));                   
        p = ori + dir * tmid;                   
    	float hmid = map(p);
		if(hmid < 0.0) {
        	tx = tmid;
            hx = hmid;
        } else {
            tm = tmid;
            hm = hmid;
        }
    }
    return tmid;
}

// main
void mainImage( out vec4 fragColor, in vec2 fragCoord ) {
	vec2 uv = fragCoord.xy / iResolution.xy;
    uv = uv * 2.0 - 1.0;
    uv.x *= iResolution.x / iResolution.y;    
    float time = iGlobalTime * 0.3 + iMouse.x*0.01;
        
    // ray
    vec3 ang = vec3(sin(time*3.0)*0.1,sin(time)*0.2+0.3,time);    
    vec3 ori = vec3(0.0,3.5,time*5.0);
    vec3 dir = normalize(vec3(uv.xy,-2.0)); dir.z += length(uv) * 0.15;
    dir = normalize(dir) * fromEuler(ang);
    
    // tracing
    vec3 p;
    heightMapTracing(ori,dir,p);
    vec3 dist = p - ori;
    vec3 n = getNormal(p, dot(dist,dist) * EPSILON_NRM);
    vec3 light = normalize(vec3(0.0,1.0,0.8)); 
             
    // color
    vec3 color = mix(
        getSkyColor(dir),
        getSeaColor(p,n,light,dir,dist),
    	pow(smoothstep(0.0,-0.05,dir.y),0.3));
        
    // post
	fragColor = vec4(pow(color,vec3(0.75)), 1.0);
}













</script>
<script type="text/javascript">

var glpixelbuffer = new Array(100000);
var timeOn = true;

window.addEventListener('load', function() {

	/*
	Shader Inputs
	uniform vec3      iResolution;           // viewport resolution (in pixels)
	uniform float     iGlobalTime;           // shader playback time (in seconds)
	uniform float     iChannelTime[4];       // channel playback time (in seconds)
	uniform vec3      iChannelResolution[4]; // channel resolution (in pixels)
	uniform vec4      iMouse;                // mouse pixel coords. xy: current (if MLB down), zw: click
	uniform samplerXX iChannel0..3;          // input channel. XX = 2D/Cube
	uniform vec4      iDate;                 // (year, month, day, time in seconds)
	uniform float     iSampleRate;           // sound sample rate (i.e., 44100)
	*/

	// Get A WebGL context
	var canvas = document.getElementById("canvas");
	var errormessage = document.getElementById("errormessage");
	var gl = getWebGLContext(canvas);
	if (!gl) {
		return;
	}

	var fragPrefix =
		"precision mediump float; "
		+"uniform vec3 iResolution; "
		+"uniform float iGlobalTime; "
		+"uniform vec4 iMouse; "
		// +"uniform sampler2D texture;\n"
		;

	var fragSuffix = "\n"
		+"void main() {\n"
		+"  vec4 tmp = vec4(0.0, 0.0, 0.0, 1.0);\n"
		+"  mainImage(tmp, gl_FragCoord.xy);\n"
		+"  gl_FragColor = tmp;\n"
		+"}\n"
		+"\n"
		;


	// setup GLSL program
	program = null;
	try {
		vertexShader = createShaderFromScriptElement(gl, "2d-vertex-shader", null, null, '', '');
		fragmentShader = createShaderFromScriptElement(gl, "2d-fragment-shader", null, null, fragPrefix, fragSuffix);
		program = createProgram(gl, [vertexShader, fragmentShader]);
	} catch(e) {
		console.log('error', e);
		errormessage.innerText = e;
		errormessage.className = 'visible';
	}

	if (!program) {
		console.log('Error detected.');
		return;
	}

	gl.useProgram(program);

	// look up where the vertex data needs to go.
	var positionLocation = gl.getAttribLocation(program, "position");

	var resolutionLocation = gl.getUniformLocation(program, "iResolution");
	var timeLocation = gl.getUniformLocation(program, "iGlobalTime");
	var mouseLocation = gl.getUniformLocation(program, "iMouse");

	// Create a buffer and put a single clipspace rectangle in
	// it (2 triangles)
	var buffer = gl.createBuffer();
	gl.bindBuffer(gl.ARRAY_BUFFER, buffer);
	gl.bufferData(gl.ARRAY_BUFFER, new Float32Array([
		-1.0, -1.0, 1.0, -1.0, -1.0,  1.0,
		-1.0,  1.0, 1.0, -1.0, 1.0,  1.0]), gl.STATIC_DRAW);
	gl.enableVertexAttribArray(positionLocation);
	gl.vertexAttribPointer(positionLocation, 2, gl.FLOAT, false, 0, 0);

	function resize() {
		var w = window.innerWidth;
		var h = window.innerHeight;
		w = 320;
		h = 64;
		canvas.width = w;
		canvas.height = h;
		gl.uniform3f(resolutionLocation, w, h, 0);
		gl.viewport(0, 0, w, h);
	}

	function mousemove(e) {
		var w = window.innerWidth;
		var h = window.innerHeight;
		gl.uniform4f(mouseLocation, e.offsetX, e.offsetY, 1, 1);
	}

	function renderFrame() {
		gl.uniform1f(timeLocation, time);
		gl.drawArrays(gl.TRIANGLES, 0, 6);


		var buf = new Uint8Array(100000);
		gl.readPixels(0, 0, 320, 64, gl.RGBA, gl.UNSIGNED_BYTE, buf);
		// console.log(buf);
		for(var j=0; j<rows; j++) {
			for(var i=0; i<cols; i++) {
				var oo = ((j * cols) + i) * 3;
				var x = i * 2;
				var y = j * 2;
				var io = (((canvas.height - 1 - y) * canvas.width) + x) * 4;
				glpixelbuffer[oo] = buf[io+0];
				glpixelbuffer[oo+1] = buf[io+1];
				glpixelbuffer[oo+2] = buf[io+2];
			}
		}

		if (timeOn)
			time += 1.0 / 60.0;
		requestAnimFrame(renderFrame);
	}

	var time = 0.0;

	window.addEventListener('mousemove', mousemove);
	window.addEventListener('resize', resize);

	resize();
	requestAnimFrame(renderFrame);
});

function mapppp(x, xx, yy) {
	x = 128 + 100 * Math.sin(
		2 * Math.sin(Math.cos(x + xx/27) * 3) +
		2 * Math.cos((x + yy / 50) * 1.77) +
		2 * Math.sin(x / 4 + xx / 35 + yy / 31)
	);
	x /= 100;
	x *= x;
	x *= x;
	x = Math.round(x);
	x >>= 2;
	x <<= 1;
	if (x < 0) x = 0;
	if (x > 255) x = 255;
	return x;
}

function initCanvas() {
	var canvas = document.getElementById('c1');
	canvas.height = rows * 8;
	canvas.width = cols * 8;
	ctx = canvas.getContext("2d");
	canvas.addEventListener('mousemove', ev_mousemove, false);
	canvas.addEventListener('mousedown', ev_mousedown, false);
	canvas.addEventListener('mouseup', ev_mouseup, false);
}

var _gamma2 = function(r) {
	return (r * r) / 256;
}


function prepareFrame(callback) {
	for(var j=0; j<rows; j++) {
		for(var i=0; i<cols; i++) {
			var o = (j * cols + i) * 3;
			var _r = glpixelbuffer[o + 0];
			var _g = glpixelbuffer[o + 1];
			var _b = glpixelbuffer[o + 2];
			// _r = i;
			// _g = 0; // (i % 32) * 256 / 132;
			// _b = 0;
			//if (j > 10) {
			//	_r = 0;
			//	_g = 0;
			//	_b = 0;
			// }

			//_r = (_r + _g + _b) / 3;
			//if (_r > 255) _r = 255;
			//_g = _r;
			//_b = _r;

		//	_r = _gamma2(_r);
		//	_g = _gamma2(_g);
		//	_b = _gamma2(_b);

			framebuffer[o + 0] = _r;
			framebuffer[o + 1] = _g;
			framebuffer[o + 2] = _b;
		}
	}

	callback();
}

document.getElementById('startstop').addEventListener('click', function() {
	timeOn = !timeOn;
});

document.getElementById('scanlines').value = queueLines.toString();
document.getElementById('scaninterval').value = queueInterval.toString();

document.getElementById('scanlines').addEventListener('change', function() {
	var v = parseInt(document.getElementById('scanlines').value, 10);
	queueLines = v;
	console.log('set queueLines to ' + v);
});

document.getElementById('scaninterval').addEventListener('change', function() {
	var v = parseInt(document.getElementById('scaninterval').value, 10);
	queueInterval = v;
	console.log('set queueInterval to ' + v);
});

initLedpanel(prepareFrame);

</script>
</body>
</html>